import com.filipmalczak.recordtuples.ComparatorsSource
import com.filipmalczak.recordtuples.FsLayout
import com.filipmalczak.recordtuples.TupleSource
import com.github.filipmalczak.recordtuples.TupleDefinition

plugins {
    id 'java-library'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

group = "com.github.filipmalczak"
version = '0.3.0-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.0.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.0.3'
}

task generateTupleSources(group: "generation", description: "Generate the actual sources") {
    doLast {
        def definitions = TupleDefinition.load(project.&property)
        def basePkg = project.property("basePackage")
        def layout = new FsLayout(project.rootDir, basePkg)
        definitions.each {
            layout.sourceFile(it.name()).withWriter { w ->
                w.print(new TupleSource(basePkg, it))
            }
        }
        layout.sourceFile("TupleComparators").withWriter { w ->
            w.print(new ComparatorsSource(basePkg, definitions))
        }
    }
}

clean {
    delete new FsLayout(project.rootDir, project.property("basePackage")).generatedSourcesDir
}

compileJava.dependsOn generateTupleSources

sourceSets {
    main {
        java {
            srcDir new FsLayout(project.rootDir, project.property("basePackage")).generatedSourcesDir
        }
    }
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = "7.4.2"
    distributionType = Wrapper.DistributionType.ALL
}
